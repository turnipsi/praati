#!/usr/bin/perl -T
# -*- mode: perl; coding: utf-8; -*-
# $Id: test,v 1.2 2014/03/31 19:08:58 je Exp $

use autodie;
# use diagnostics;
use strict;
use utf8;
use warnings FATAL => qw(all);

use Test::More tests => 3;

use Test::WWW::Mechanize;


package Praati::User {
  use Class::Struct __PACKAGE__, {
    browser  => 'Test::WWW::Mechanize',
    email    => '$',
    name     => '$',
    password => '$',
  };

  use Test::More;

  sub new_user {
    my ($class, $page, $email, $name, $password) = @_;

    my $self = $class->new(browser  => Test::WWW::Mechanize->new(autolint => 1),
                           email    => $email,
                           name     => $name,
                           password => $password);

    $self->register($page);
  }

  sub register {
    my ($self, $page) = @_;

    my $user_name = $self->name;

    subtest(qq{Make a new user "$user_name"} => sub {
      plan(tests => 5);

      $self->browser->get_ok($page);
      $self->browser->content_contains('The main page.');

      $self->browser->follow_link_ok({ text => 'New user' });

      my $formfields = {
                         user_email          => $self->email,
                         user_name           => $self->name,
                         user_password       => $self->password,
                         user_password_again => $self->password,
                       };

      $self->browser->submit_form_ok({
                                       button      => 'submit_new_user',
                                       with_fields => $formfields,
                                     },
                                     'Send new user form');

      $self->browser->base_like(qr{login$});
    });
  }
}


#
# main
#

my $page = 'http://localhost/~je/praati/praati';

my $user1 = Praati::User->new_user($page,
                                   'user1@example.org',
                                   'User 1',
                                   'pogjwviwjvalaoo');

my $user2 = Praati::User->new_user($page,
                                   'user2@example.org',
                                   'User 2',
                                   'jtreofnvwlcjiwe');

my $user3 = Praati::User->new_user($page,
                                   'user3@example.org',
                                   'User 3',
                                   'ofwqhowqjbnmqwo');
