#!/usr/bin/perl -T
# -*- mode: perl; coding: utf-8; -*-
# $Id: test,v 1.6 2014/04/03 18:05:49 je Exp $

use autodie;
# use diagnostics;
use strict;
use utf8;
use warnings FATAL => qw(all);

use HTML::TableContentParser;
use Test::More tests => 5;
use Test::WWW::Mechanize;


package Praati::User {
  use Class::Struct __PACKAGE__, {
    browser  => 'Test::WWW::Mechanize',
    email    => '$',
    name     => '$',
    password => '$',
    site     => '$',
  };

  use Test::More;

  sub new_user {
    my ($class, $site, $email, $name, $password) = @_;

    my $self = $class->new(browser  => Test::WWW::Mechanize->new(autolint => 1),
                           email    => $email,
                           name     => $name,
                           password => $password,
                           site     => $site);

    $self->register();

    $self;
  }

  sub login {
    my ($self, $user_type) = @_;

    my $user_name = $self->name;

    subtest(qq{Login user "$user_name"} => sub {
      plan(tests => 4);
      $self->main_page_ok();
      $self->browser->follow_link_ok({ text => 'Login' });

      my $formfields = {
                         user_email    => $self->email,
                         user_password => $self->password,
                       };

      $self->browser->submit_form_ok({
                                       button      => 'submit_login',
                                       with_fields => $formfields,
                                     },
                                     'Send login form');

      $self->browser->base_like(qr{main$});

      # XXX should check user type here (that it matches $user_type)
    });
  }

  sub lookup_rating_id_number_for_song {
    my ($self, $songname) = @_;

    my $tcp   = HTML::TableContentParser->new();
    my $table = $tcp->parse( $self->browser->content() );

    foreach my $row (@{ $table->[1]{rows} }) {
      my $cells = $row->{cells};
      if ($cells
            && $cells->[1]
            && $cells->[1]{data}
            && $cells->[1]{data} eq $songname
            && $cells->[2]
            && $cells->[2]{data}) {
        if ($cells->[2]{data} =~ /^<select name="songs\[(\d+)\].rating_value"/) {
          return $1;
        }
      }
    }

    return;
  }

  sub main_page_ok {
    my ($self) = @_;
    $self->browser->get_ok($self->site);
  }

  sub rate_songs {
    my ($self, $panel_name, $song_ratings) = @_;

    my $user_name = $self->name;

    my $title = qq{"$user_name" rates songs in "$panel_name"};

    my $song_ratings_count = scalar(keys %$song_ratings);

    subtest($title => sub {
      plan(tests => (4 + $song_ratings_count));

      $self->main_page_ok();
      $self->browser->follow_link_ok({ text => 'Panels' });
      $self->browser->follow_link_ok({ text => $panel_name });

      $self->browser->form_number(1);

      while (my ($song_name, $rating_value) = each %$song_ratings) {
        my $rating_id = $self->lookup_rating_id_number_for_song($song_name);

        ok(defined($rating_id), "Found rating id for song '$song_name'");

        $self->browser->select("songs[$rating_id].rating_value",
                               $rating_value);
      }

      $self->browser->submit_form_ok({ button => 'send_ratings', },
                                     'Send ratings');
    });
  }

  sub register {
    my ($self) = @_;

    my $user_name = $self->name;

    subtest(qq{Make a new user "$user_name"} => sub {
      plan(tests => 5);

      $self->main_page_ok();
      $self->browser->content_contains('The main page.');

      $self->browser->follow_link_ok({ text => 'New user' });

      my $formfields = {
                         user_email          => $self->email,
                         user_name           => $self->name,
                         user_password       => $self->password,
                         user_password_again => $self->password,
                       };

      $self->browser->submit_form_ok({
                                       button      => 'submit_new_user',
                                       with_fields => $formfields,
                                     },
                                     'Send new user form');

      $self->browser->base_like(qr{login$});
    });
  }
}


#
# main
#

my $site = 'http://localhost/~je/praati/praati';

my $admin1 = Praati::User->new_user($site,
                                    'admin1@example.org',
                                    'Admin One',
                                    'pogjwviwjvalaook');

my $critic1 = Praati::User->new_user($site,
                                    'critic1@example.org',
                                    'Critic One',
                                    'jtreofnvwlcjiwe');

my $critic2 = Praati::User->new_user($site,
                                    'critic2@example.org',
                                    'Critic Two',
                                    'ofwqhowqjbnmqwo');

$critic1->login('critic');
$critic1->rate_songs('Depeche Mode - Best of Greatest Hits',
                     {
                       'Everything Counts' => '7.5',
                       'Personal Jesus'    => '8.0',
                       'Shake The Disease' => '3.5',
                     });
