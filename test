#!/usr/bin/perl -T
# -*- mode: perl; coding: utf-8; -*-
# $Id: test,v 1.4 2014/04/01 19:05:46 je Exp $

use autodie;
# use diagnostics;
use strict;
use utf8;
use warnings FATAL => qw(all);

use HTML::TableContentParser;
use Test::More tests => 5;
use Test::WWW::Mechanize;


package Praati::User {
  use Class::Struct __PACKAGE__, {
    browser  => 'Test::WWW::Mechanize',
    email    => '$',
    name     => '$',
    password => '$',
    site     => '$',
  };

  use Data::Dumper;
  use Test::More;

  sub new_user {
    my ($class, $site, $email, $name, $password) = @_;

    my $self = $class->new(browser  => Test::WWW::Mechanize->new(autolint => 1),
                           email    => $email,
                           name     => $name,
                           password => $password,
                           site     => $site);

    $self->register($self->site);

    $self;
  }

  sub login {
    my ($self) = @_;

    my $user_name = $self->name;

    subtest(qq{Login user "$user_name"} => sub {
      plan(tests => 4);
      $self->main_page_ok();
      $self->browser->follow_link_ok({ text => 'Login' });

      my $formfields = {
                         user_email    => $self->email,
                         user_password => $self->password,
                       };

      $self->browser->submit_form_ok({
                                       button      => 'submit_login',
                                       with_fields => $formfields,
                                     },
                                     'Send login form');

      $self->browser->base_like(qr{main$});
    });
  }

  sub main_page_ok {
    my ($self) = @_;
    $self->browser->get_ok($self->site);
  }

  sub lookup_rating_id_number_for_song {
    my ($self, $songname) = @_;

    my $tcp = HTML::TableContentParser->new();

    my $table = $tcp->parse( $self->browser->content() );

    foreach my $row (@{ $table->[1]{rows} }) {
      my $cells = $row->{cells};
      if ($cells->[1]{data} eq $songname) {
        # XXX regexp, blah!
        if ($cells->[2]{data} =~ /songs\[(\d+)\].rating_value/) {
          return $1;
        }
      }
    }

    return;
  }

  sub rate_songs {
    my ($self, $panel_name) = @_;

    my $user_name = $self->name;

    my $title = qq{"$user_name" rates songs in "$panel_name"};

    subtest($title => sub {
      plan(tests => 4);
      $self->main_page_ok();
      $self->browser->follow_link_ok({ text => 'Panels' });
      $self->browser->follow_link_ok({ text => $panel_name });

      my $rating_id
        = $self->lookup_rating_id_number_for_song('Everything Counts');

      $self->browser->form_number(1);
      $self->browser->select("songs[$rating_id].rating_value", "7.5");
      $self->browser->submit_form_ok({ button => 'send_ratings', },
                                     'Send ratings');
    });
  }

  sub register {
    my ($self) = @_;

    my $user_name = $self->name;

    subtest(qq{Make a new user "$user_name"} => sub {
      plan(tests => 5);

      $self->main_page_ok();
      $self->browser->content_contains('The main page.');

      $self->browser->follow_link_ok({ text => 'New user' });

      my $formfields = {
                         user_email          => $self->email,
                         user_name           => $self->name,
                         user_password       => $self->password,
                         user_password_again => $self->password,
                       };

      $self->browser->submit_form_ok({
                                       button      => 'submit_new_user',
                                       with_fields => $formfields,
                                     },
                                     'Send new user form');

      $self->browser->base_like(qr{login$});
    });
  }
}


#
# main
#

my $site = 'http://localhost/~je/praati/praati';

my $user1 = Praati::User->new_user($site,
                                   'user1@example.org',
                                   'User 1',
                                   'pogjwviwjvalaook');

my $user2 = Praati::User->new_user($site,
                                   'user2@example.org',
                                   'User 2',
                                   'jtreofnvwlcjiwe');

my $user3 = Praati::User->new_user($site,
                                   'user3@example.org',
                                   'User 3',
                                   'ofwqhowqjbnmqwo');

$user1->login();
$user1->rate_songs("Depeche Mode - Best of Greatest Hits");
